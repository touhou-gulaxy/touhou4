utility_component_template = {
    key = "spth_forerunner_reactor"
    size = small
    icon = "GFX_spth_ship_part_forerunner_reactor"
    icon_frame = 1
    power = 750000

    resources = {
        category = ship_components
        cost = {
            alloys = 75
            sr_fuka = 10
        }
        upkeep = {
            energy = 5
            alloys = 2
        }
    }

    component_set = "power_core"
    size_restriction = { spth_ur_didact_flagship }

    ai_weight = {
        weight = 1000
    }
}
utility_component_template = {
    key = "spth_forerunner_slipspace_drive"
    size = small
    icon = "GFX_spth_ship_part_forerunner_slipspace_drive"
    icon_frame = 1
    power = -2500
    resources = {
        category = ship_components
        cost = {
            alloys = 20
        }
    }
    ftl = yes
    jumpdrive = yes
    ship_modifier = {
        ship_windup_mult = -1.0
        ship_ftl_jumpdrive_range_mult = 3.0
        ship_jumpdrive_cooldown_mult = -1.0
    }

    ai_weight = {
        weight = 1000
    }

    size_restriction = { spth_ur_didact_flagship }
    component_set = "ftl_components"
}
utility_component_template = {
    key = "spth_forerunner_ship_ancilla"
    size = small
    icon = "GFX_spth_ship_part_foreruner_mendicant_bias"
    icon_frame = 1
    power = -2500
    resources = {
        category = ship_components
        cost = {
            sr_fuka = 5
            alloys = 20
        }
    }
    size_restriction = { spth_ur_didact_flagship }
    component_set = "combat_computers"
    ship_behavior = "spth_mantel_approach_attack"

    modifier = {
        ship_weapon_damage = 2.5
        ship_weapon_range_mult = 0.2
        ship_cloaking_detection_add = 10
    }
    friendly_aura = {
        name = mendicant_bias_buff
        stack_info = {
            id = mendicant_bias_buff
            priority = 1002
        }
        apply_on = ships
        radius = 225
        modifier = {
            ship_evasion_add = 20
            ship_weapon_damage = 0.25
        }
        graphics = {
            area_effect = { entity = "circle_area_entity" dynamic_scale = yes }
            ship_effect = { entity = "ship_aura_positive_entity" dynamic_scale = no }
        }
    }
    hostile_aura = {
        name = mendicant_bias_debuff
        stack_info = {
            id = mendicant_bias_debuff
            priority = 1002
        }
        apply_on = ships
        radius = 225
        modifier = {
            ship_evasion_add = -20
            ship_accuracy_mult = -20
            ship_tracking_add = -20
            ship_disengage_opportunities_add = -6
        }
        graphics = {
            area_effect = { entity = "circle_area_entity" dynamic_scale = yes }
            ship_effect = { entity = "ship_aura_positive_entity" dynamic_scale = no }
        }
    }

    ai_weight = {
        weight = 1000
    }
}
utility_component_template = {
    key = "spth_sensor_t0"
    size = medium
    icon = "GFX_spth_ship_part_sensor_t0"
    icon_frame = 1
    power = -2000
    resources = {
        category = ship_components
        cost = {
            alloys = 20
        }
    }
    sensor_range = 10
    hyperlane_range = 5

    ship_modifier = {
        ship_accuracy_add = 40
        ship_tracking_add = 20
    }

    size_restriction = { spth_ur_didact_flagship }
    component_set = "sensor_components"

    ai_weight = {
        weight = 1000
    }
}
utility_component_template = {
    key = "spth_reaction_driver"
    size = small
    icon = "GFX_ship_part_wormhole"
    icon_frame = 1
    power = -2500
    resources = {
        category = ship_components
        cost = {
            alloys = 100
            sr_fuka = 25
        }
        upkeep = {
            energy = 8
            sr_fuka = 0.5
        }
    }

    modifier = {
        ship_evasion_add = 10
    }

    component_set = "thruster_components"
    size_restriction = { spth_ur_didact_flagship }

    ai_weight = {
        weight = 5
    }
}
weapon_component_template = {
    key = "spth_z8060_particle_cannon"
    size = point_defence
    entity = "invisible_turret_entity"

    icon = "GFX_spth_ship_part_z8060_particle_cannon"
    icon_frame = 1
    type = point_defence

    static_rotation = yes
    use_ship_kill_target = no

    damage = { min = 64 max = 64 }
    windup = { min = 5 max = 6 }
    total_fire_time = 5
    range = 75
    accuracy = 0.8
    tracking = 0.7
    shield_damage = 1
    armor_damage = 1
    hull_damage = 1
    power = -100

    component_set = "spth_z8060_particle_cannon"
    size_restriction = { spth_ur_didact_flagship }
    projectile_gfx = "blue_laser_pd"
    tags = { weapon_type_point_defense weapon_type_energy weapon_type_lingli }
    ai_tags = { weapon_role_point_defense }
    point_defence_targets = { "missile" "strike_craft" }
    ai_tag_weight = 999
    resources = {
        category = ship_components
        cost = {
            alloys = 12
            sr_fuka = 1
        }
        upkeep = {
            energy = 1.4
            sr_lingli = 0.01
        }
    }

    ai_weight = {
        weight = 999
        modifier = {
            factor = 0.0
            has_monthly_income = { resource = sr_fuka value <= 0 }
        }
    }
}
weapon_component_template = {
    key = "spth_z8260_artillery"
    size = large
    entity = "tinvisible_turret_entity"
    type = instant
    prio_projectile = yes
    icon = "GFX_spth_ship_part_z8260_artillery"
    icon_frame = 1

    static_rotation = yes
    use_ship_kill_target = no

    damage = { min = 896 max = 896 }
    windup = { min = 5 max = 15 }
    total_fire_time = 25
    range = 300.0
    accuracy = 1.0
    tracking = 1.0
    shield_penetration = 1.0
    armor_penetration = 1.0
    power = -120

    size_restriction = { spth_ur_didact_flagship }
    component_set = "spth_z8260_artillery"
    projectile_gfx = "adv_kinetic_artillery"
    tags = { weapon_type_energy weapon_type_kinetic weapon_type_spth_hardlight }
    ai_tags = { weapon_role_anti_shield weapon_role_anti_armor }
    ai_tag_weight = 1000
    resources = {
        category = ship_components
        cost = {
            alloys = 42
            sr_fuka = 3
        }
        upkeep = {
            energy = 1.8
            sr_lingli = 0.09
        }
    }

    ai_weight = {
        weight = 1000
        modifier = {
            factor = 0.0
            has_monthly_income = { resource = sr_fuka value <= 0 }
        }
    }
}
weapon_component_template = {
    key = "spth_z8260_heavy_artillery"
    size = extra_large
    entity = "tinvisible_turret_entity"
    type = instant
    prio_projectile = yes
    icon = "GFX_spth_ship_part_z8260_artillery_heavy"
    icon_frame = 1

    static_rotation = yes
    use_ship_kill_target = no

    damage = { min = 10240 max = 10240 }
    windup = { min = 5 max = 15 }
    total_fire_time = 90
    range = 400.0
    accuracy = 1.0
    tracking = 1.0
    shield_penetration = 2.0
    armor_penetration = 2.0
    hull_damage = 1.5
    power = -240

    size_restriction = { spth_ur_didact_flagship }
    component_set = "spth_z8260_heavy_artillery"
    projectile_gfx = "adv_kinetic_artillery"
    tags = { weapon_type_energy weapon_type_kinetic weapon_type_spth_hardlight }
    ai_tags = { weapon_role_anti_hull artillery_stealth artillery weapon_role_artillery }
    ai_tag_weight = 1000
    resources = {
        category = ship_components
        cost = {
            alloys = 42
            sr_fuka = 3
        }
        upkeep = {
            energy = 1.8
            sr_lingli = 0.09
        }
    }

    ai_weight = {
        weight = 1000
        modifier = {
            factor = 0.0
            has_monthly_income = { resource = sr_fuka value <= 0 }
        }
    }
}
weapon_component_template = {
    key = "spth_z8260_ultra_heavy_artillery"
    size = titanic
    entity = "tinvisible_turret_entity"
    type = instant
    prio_projectile = yes
    icon = "GFX_spth_ship_part_z8260_artillery_ultra_heavy"
    icon_frame = 1
    ship_limit = 1

    # static_rotation = yes
    use_ship_kill_target = no
    damage = { min = 1000 max = 1000 }
    firing_arc = 60
    windup = { min = 0 max = 800 }
    total_fire_time = 31600
    range = 320.0
    accuracy = 1.0
    tracking = 1.0
    shield_penetration = 10.0
    armor_penetration = 10.0
    power = -50000

    ship_modifier = {
        custom_tooltip = spth_z8260_ultra_heavy_artillery_tooltip
        weapon_type_spth_strange_matter_weapon_damage_mult = 99999
        weapon_type_spth_strange_matter_weapon_fire_rate_mult = 99
    }
    size_restriction = { spth_ur_didact_flagship }
    component_set = "spth_z8260_ultra_heavy_artillery"
    projectile_gfx = "synth_queen_01_perdition_beam"
    tags = { weapon_type_spth_strange_matter }
    ai_tags = { weapon_role_anti_hull }
    ai_tag_weight = 1000
    resources = {
        category = ship_components
        cost = {
            alloys = 420
            sr_fuka = 30
        }
        upkeep = {
            energy = 18
            sr_lingli = 0.9
        }
    }

    ai_weight = {
        weight = 1000
        modifier = {
            factor = 0.0
            has_monthly_income = { resource = sr_fuka value <= 0 }
        }
    }
}
weapon_component_template = {
    key = "spth_composer"
    size = planet_killer
    type = planet_killer
    use_ship_kill_target = no

    power = -75000
    windup = {
        min = 60
        max = 120
    }
    total_fire_time = 60
    accuracy = 1
    damage = {
        min = 0
        max = 0
    }
    range = 0
    resources = {
        category = ship_components
        cost = {
            sr_lingli = 1000
            sr_fuka = 1000
        }
        upkeep = {
            sr_lingli = 10
            sr_fuka = 10
        }
    }
    icon = "GFX_spth_ship_part_composer"
    icon_frame = 1
    size_restriction = {
        spth_ur_didact_flagship
    }
    component_set = "spth_composer"

    planet_destruction_gfx = "neutron_planet_gfx"

    ai_weight = {
        factor = 30
    }
}
